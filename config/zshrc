export NH_HOME_FLAKE="/home/brian/.config/home-manager"
export PAGER="ov"
export SYSTEMD_PAGER="ov"
export SYSTEMD_SECURE="true"
export MANPAGER='vim +Man!'
export EDITOR="vim"
export VISUAL="vim"
export TERMINAL="kitty"

eval "$(zoxide init zsh)"

# Edit command in vim with <C-x><C-e>
autoload edit-command-line
zle -N edit-command-line
bindkey -M vicmd "^X^E" edit-command-line
bindkey -M viins "^X^E" edit-command-line


function playerctl() {
	eval `systemctl --user show-environment | grep CURRENT_PLAYER`
	env playerctl -p "$CURRENT_PLAYER" $*
}

# detach from tmux instead of killing the session.
# This could use some work to only detach if the session has 1 pane.
# As the simple way to kill a pane is "Ctrl-d".
stty eof '' # let zsh handle ctrl-d
function smart_ctrl_d() {
	if [[ -n $IN_NIX_SHELL ]]; then
		exit
	elif [[ -n $NVIM ]]; then
		exit
	elif [[ -n $TMUX ]]; then
		tmux detach
	else
		exit
	fi
}
zle -N smart_ctrl_d
bindkey '^D' smart_ctrl_d

if [[ $- == *i* ]]; then
	if [[ -n $NEOVIDE ]]; then
		zstyle ':fzf-tab:*' fzf-command fzf
		unset TMUX # Not sure if something needs tmux to be set?
	elif [[ -n $TMUX ]]; then
		zstyle ':fzf-tab:*' fzf-command ftb-tmux-popup
	elif [[ -o login ]]; then
		# do not start tmux on the login shell
	else
		tmuxcmd=""
		nbsession=$(tmux ls 2> /dev/null | wc -l)
		if [[ $nbsession -eq 0 ]]; then
			tmuxcmd='tmux -2 new-session -t Main'
		elif [[ $nbsession -lt 5 ]]; then
			# If a session is not attached too, attach to it.
			tmuxcmd='tmux -2 new-session'
			tmuxsession=$(tmux ls 2> /dev/null)
			for session in ${(f)tmuxsession}; do
				local name=$(echo $session | cut -d ":" -f 1)
				clients=$(tmux list-clients -t $name 2> /dev/null | wc -l)
				if [[ $clients -eq 0 ]]; then
					# If it's not attached, attach to it
					tmuxcmd="tmux attach -t $name"
				fi;
			done
		else
			tmuxcmd='tmux -2 attach'
		fi

		eval "$tmuxcmd"
	fi
fi

#To be hooked with nix shells
eval "$CMD"
